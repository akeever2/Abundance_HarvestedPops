
    model {


    ### PRIORS ###______________________________________________________________


    ## Population size___________________
    
    # Initial population size for each age class (a) and sex (s) by DMU (i). Ages are 0.5
    # (fawns), 1.5 (yearlings), and 2.5+ (adults). For s, female = 1 and male = 2
    # N[age, year, sex, DMU]
    for(a in 1:nages){
      for(s in 1:2){
        for(i in 1:nDMUs){
          N[a,1,s,i] ~ dnorm(initN[a,s,i], 1 / (initN[a,s,i]))T(0,)
        } # end DMU loop
      } # end sex loop
    } # end age loop



    ## Demographic rates___________________
    
    # Mean demographic rates - values for logit scale
    for(a in 1:nages){
      for(s in 1:2){
        
        # Natural survival (NS)
        mu.NS[a,s] ~ dnorm(ns.shape[a,1,s], pow(ns.shape[a,2,s], -2))T(-10,10)
        
        # Hunting season survival (HS)
        mu.HS[a,s] ~ dnorm(hs.shape[a,1,s], pow(hs.shape[a,2,s], -2))T(-10,10)
        
        for(p in 1:nperiods){
          # Reporting rates
          mu.Report[a,s,p] ~ dnorm(report.shape[a,1,s,p], pow(report.shape[a,2,s,p], -2))T(-10,10)
          
        } # end period loop
      } # end sex loop
    } # end age loop


    # Mean neonate survival (S0) - values on logit scale
      mu.S0 ~ dnorm(s0.shape[1,1], s0.shape[1,2])T(-10,10)


    # Components for fertility (F): productivity (m), sex ratio (s.ratio), survival
    # Productivity (fawns/doe) on real scale
    for(a in 1:nages){
      m[a] ~ dgamma(m.shape[a,1], m.shape[a,2])T(0,5)
    } # end age loop


    # Sex ratio: 1 = probability of being female, 2 = probability of being male
    s.ratio[1] <- 0.5
    s.ratio[2] <- 1 - s.ratio[1]
    
    
    # SDs for random year and site effects on demographic rates
    for(a in 1:nages){
      for(s in 1:2){
        sd.NS.yr[a,s] ~ dgamma(sd.yr.NS[a,1,s], sd.yr.NS[a,2,s])T(0,5)
        sd.HS.yr[a,s] ~ dgamma(sd.yr.HS[a,1,s], sd.yr.HS[a,2,s])T(0,5)
        sd.Report.yr[a,s] ~ dgamma(sd.yr.report[a,1,s], sd.yr.report[a,2,s])T(0,5)
        
        
        sd.NS.dmu[a,s] ~ dgamma(sd.dmu.NS[a,1,s], sd.dmu.NS[a,2,s])T(0,5)
        sd.HS.dmu[a,s] ~ dgamma(sd.dmu.HS[a,1,s], sd.dmu.HS[a,2,s])T(0,5)
        sd.Report.dmu[a,s] ~ dgamma(sd.dmu.report[a,1,s], sd.dmu.report[a,2,s])T(0,5)
      }
    }
    
    sd.S0.yr ~ dgamma(sd.yr.S0[1,1], sd.yr.S0[1,2])T(0,5)
    sd.S0.dmu ~ dgamma(sd.dmu.S0[1,1], sd.dmu.S0[1,2])T(0,5)
    
    
    # Random year effects for demographic rates
    for(k in 1:(nyears + npredyrs)){
      for(a in 1:nages){
          eps.NS.yr[a,k] ~ dnorm(0, pow(sd.NS.yr[a,1], -2))T(-5,5)
          eps.HS.yr[a,k] ~ dnorm(0, pow(sd.HS.yr[a,1], -2))T(-5,5)
        for(s in 1:2){
          eps.Report.yr[a,k,s] ~ dnorm(0, pow(sd.Report.yr[a,s], -2))T(-5,5)
        } # end sex loop
      } # end age loop
      
      eps.S0.yr[k] ~ dnorm(0, pow(sd.S0.yr, -2))T(-5,5)
    } # end year loop
    
    
    # Random site effects for demographic rates
    for(i in 1:nDMUs){
      for(a in 1:nages){
          eps.NS.dmu[a,i] ~ dnorm(0, pow(sd.NS.dmu[a,1], -2))T(-5,5)
          eps.HS.dmu[a,i] ~ dnorm(0, pow(sd.HS.dmu[a,1], -2))T(-5,5)
        for(s in 1:2){
          eps.Report.dmu[a,s,i] ~ dnorm(0, pow(sd.Report.dmu[a,s], -2))T(-5,5)
        } # end sex loop
      } # end age loop
      
      eps.S0.dmu[i] ~ dnorm(0, pow(sd.S0.dmu, -2))T(-5,5)
    } # end DMU loop
    
    
    # Demogrphic rates on real scale 
    for(k in 1:(nyears + npredyrs)){
      for(i in 1:nDMUs){
        for(a in 1:nages){
          for(s in 1:2){
            logit(NS[a,k,s,i]) <- mu.NS[a,s] + eps.NS.yr[a,k] + eps.NS.dmu[a,i]
            logit(HS[a,k,s,i]) <- mu.HS[a,s] + eps.HS.yr[a,k] + eps.HS.dmu[a,i]
            logit(Report[a,k,s,i]) <- mu.Report[a,s,Period[k]] + eps.Report.yr[a,k,s] + 
                                      eps.Report.dmu[a,s,i]
          } # end sex loop
        
        
        # Fertility  
        F[a,k,i] <- NS[a,k,1,i]^ (8 / 12) * HS[a,k,1,i] * m[a]  
        } # end age loop
        
        for(s in 1:2){
          logit(S0[k,s,i]) <- mu.S0 + eps.S0.yr[k] + eps.S0.dmu[i]
  
        } # end sex loop
      } # end DMU loop
    } # end year loop




    
    


    ### LIKELIHOOD ###__________________________________________________________
    
    
    ## Ecological Process___________________
    
    # Change in abundance over time
    for(k in 2:(nyears + npredyrs)){
      for(i in 1:nDMUs){
        for(s in 1:2){
          # Projection for fawns
          mu.fawn[k,s,i] <- N[1,k-1,1,i] * F[1,k-1,i] * s.ratio[s] * S0[k-1,s,i] + 
                            N[2,k-1,1,i] * F[2,k-1,i] * s.ratio[s] * S0[k-1,s,i] + 
                            N[3,k-1,1,i] * F[3,k-1,i] * s.ratio[s] * S0[k-1,s,i]
          N[1,k,s,i] ~ dnorm(mu.fawn[k,s,i], 1 / mu.fawn[k,s,i])T(0,)
          
          # Projection for yearlings
          mu.yearling[k,s,i] <- N[1,k-1,s,i] * HS[1,k-1,s,i] * NS[1,k-1,s,i]
          tau.yearling[k,s,i] <- 1 / ((N[1,k-1,s,i] + 1) * HS[1,k-1,s,i] * NS[1,k-1,s,i] *
            (1 - HS[1,k-1,s,i] * NS[1,k-1,s,i]))
          N[2,k,s,i] ~ dnorm(mu.yearling[k,s,i], tau.yearling[k,s,i])T(0,)

          # Projection for adults
          mu.ad1[k,s,i] <- N[2,k-1,s,i] * HS[2,k-1,s,i] * NS[2,k-1,s,i]
          tau.ad1[k,s,i] <- 1 / ((N[2,k-1,s,i] + 1) * HS[2,k-1,s,i] * NS[2,k-1,s,i] *
            (1 - HS[2,k-1,s,i] * NS[2,k-1,s,i]))
          N.yr[k,s,i] ~ dnorm(mu.ad1[k,s,i], tau.ad1[k,s,i])T(0,)
          
          mu.ad2[k,s,i] <- N[3,k-1,s,i] * HS[3,k-1,s,i] * NS[3,k-1,s,i]
          tau.ad2[k,s,i] <- 1 / ((N[3,k-1,s,i] + 1) * HS[3,k-1,s,i] * NS[3,k-1,s,i] *
            (1 - HS[3,k-1,s,i] * NS[3,k-1,s,i]))
          N.ad[k,s,i] ~ dnorm(mu.ad2[k,s,i], tau.ad2[k,s,i])T(0,)
          
          N[3,k,s,i] <- N.yr[k,s,i] + N.ad[k,s,i]


        } # end sex loop
      } # end DMU loop
    } # end year loop


    # Estimated and reported harvest process
    for(k in 1:(nyears + npredyrs)){
      for(i in 1:nDMUs){
        for(a in 1:nages){
          for(s in 1:2){
            # Harvest by age and sex
            mu.hest[a,k,s,i] <- N[a,k,s,i] * (1 - HS[a,k,s,i])
            tau.hest[a,k,s,i] <- 1 / ((N[a,k,s,i] + 1) * (1 - HS[a,k,s,i]) * (1 - (1 - HS[a,k,s,i])))
            Hest[a,k,s,i] ~ dnorm(mu.hest[a,k,s,i], tau.hest[a,k,s,i])T(0,)
            
            # Reported harvest by age and sex
            Hrep[a,k,s,i] <- Hest[a,k,s,i] * Report[a,k,s,i]

          } # end sex loop
        } # end age loop
      } # end DMU loop
    } # end year loop
    
    

    ## Observation process___________________

    # Statewide observations
    for(k in 1:nyears){
      # Observation of estimated harvest by bag type (antlerless and antlered)
      y.Hest[k,1,1] ~ dnorm(H.antlerless[k], pow(y.Hest[k,2,1], -2))
      y.Hest[k,1,2] ~ dnorm(H.yr.bucks[k] + H.ad.bucks[k],  pow(y.Hest[k,2,2], -2))
      
      # Observation of reported harvest by bag type (antlerless and antlered)
      # Antlerless is all females (Hrep[1:3,k,1,i]) and age 1 of males (Hrep[1,k,2,i])
      # Antlered is yearling and adult bucks (Hrep[2:3,k,2,i])
      mu.O[k,1] <- sum(Hrep[,k,1,]) + sum(Hrep[1,k,2,])
      mu.O[k,2] <- sum(Hrep[2:3,k,2,])
      y.H[k,1] ~ dnorm(mu.O[k,1], 1 / mu.O[k,1])
      y.H[k,2] ~ dnorm(mu.O[k,2], 1 / mu.O[k,2])

      # The proportion of each age class in harvest for bag type
      mu.A[1,k,1] <- sum(Hrep[1,k,1,]) / mu.O[k,1]
      mu.A[2,k,1] <- sum(Hrep[2,k,1,]) / mu.O[k,1]
      mu.A[3,k,1] <- sum(Hrep[3,k,1,]) / mu.O[k,1]
      mu.A[4,k,1] <- sum(Hrep[1,k,2,]) / mu.O[k,1]
      mu.A[1,k,2] <- sum(Hrep[2,k,2,]) / mu.O[k,2]
      mu.A[2,k,2] <- sum(Hrep[3,k,2,]) / mu.O[k,2]

      # Multinomial distribution for the observation of sex/age harvest
      y.Af[1:(nages + 1),k] ~ dmulti(mu.A[1:(nages + 1),k,1], O.aged[k,1])
      y.Am[1:(nages - 1),k] ~ dmulti(mu.A[1:(nages - 1),k,2], O.aged[k,2])
      
      
      # Observations by DMUs
      for(i in 1:nDMUs){
        # Observation of estimated harvest by bag type (antlerless and antlered)
        y.Hest.dmu[k,1,1,i] ~ dnorm(H.antlerless.dmu[i,k], y.Hest.dmu[k,2,1,i])
        y.Hest.dmu[k,1,2,i] ~ dnorm(H.yr.bucks.dmu[i,k] + H.ad.bucks.dmu[i,k],  y.Hest.dmu[k,2,2,i])
      
        # Observation of reported harvest by bag type (antlerless and antlered)
        # Antlerless is all females (Hrep[1:3,k,1,i]) and age 1 of males (Hrep[1,k,2,i])
        # Antlered is yearling and adult bucks (Hrep[2:3,k,2,i])
        mu.O.dmu[k,1,i] <- sum(Hrep[,k,1,i]) + Hrep[1,k,2,i]
        mu.O.dmu[k,2,i] <- sum(Hrep[2:3,k,2,i])
        y.H.dmu[k,1,i] ~ dnorm(mu.O.dmu[k,1,i], 1 / mu.O.dmu[k,1,i])
        y.H.dmu[k,2,i] ~ dnorm(mu.O.dmu[k,2,i], 1 / mu.O.dmu[k,2,i])
  
        # The proportion of each age class in harvest for the sexes
        mu.A.dmu[1,k,1,i] <- Hrep[1,k,1,i] / mu.O.dmu[k,1,i]
        mu.A.dmu[2,k,1,i] <- Hrep[2,k,1,i] / mu.O.dmu[k,1,i]
        mu.A.dmu[3,k,1,i] <- Hrep[3,k,1,i] / mu.O.dmu[k,1,i]
        mu.A.dmu[4,k,1,i] <- Hrep[1,k,2,i] / mu.O.dmu[k,1,i]
        mu.A.dmu[1,k,2,i] <- Hrep[2,k,2,i] / mu.O.dmu[k,2,i]
        mu.A.dmu[2,k,2,i] <- Hrep[3,k,2,i] / mu.O.dmu[k,2,i]
  
        # Multinomial distribution for the observation of sex/age harvest
        y.Af.dmu[1:(nages + 1),k,i] ~ dmulti(mu.A.dmu[1:(nages + 1),k,1,i], O.aged.dmu[k,1,i])
        y.Am.dmu[1:(nages - 1),k,i] ~ dmulti(mu.A.dmu[1:(nages - 1),k,2,i], O.aged.dmu[k,2,i])
        
      } # end DMU loop
    } # end year loop





    ### DERIVED PARAMETERS ###__________________________________________________
    
    
    ## Statewide ___________________

    # Population and harvest totals
    for(k in 1:(nyears + npredyrs)){
      Total[k] <- sum(N[,k,,])
      fawns[k] <- sum(N[1,k,,])
      does[k] <- sum(N[2:3,k,1,])
      bucks[k] <- sum(N[2:3,k,2,])
      rec.rate[k] <- sum(N[1,k,,]) / sum(N[2:3,k,1,])      
      H.total[k] <- sum(Hest[,k,,])
      H.antlerless[k] <- sum(Hest[2:3,k,1,]) + sum(Hest[1,k,,])
      H.yr.bucks[k] <- sum(Hest[2,k,2,])
      H.ad.bucks[k] <- sum(Hest[3,k,2,])
    } # end year loop
    
    
    # Population growth rate
    for(k in 1:(nyears + npredyrs - 1)){
      lambda[k] <- sum(N[,k+1,,]) / sum(N[,k,,])
      logla[k] <- log(lambda[k])
    }

    mlam <- exp((1 / (nyears + npredyrs - 1)) * sum(logla[1:(nyears + npredyrs - 1)]))
    
    

    ## DMU ___________________
    
    
    # Population and harvest totals by DMU
    for(k in 1:(nyears + npredyrs)){
      for(i in 1:nDMUs){
        Total.dmu[i,k] <- sum(N[,k,,i])
        fawns.dmu[i,k] <- sum(N[1,k,,i])
        does.dmu[i,k] <- sum(N[2:3,k,1,i])
        bucks.dmu[i,k] <- sum(N[2:3,k,2,i])
        rec.rate.dmu[i,k] <- sum(N[1,k,,i]) / sum(N[2:3,k,1,i])
        H.total.dmu[i,k] <- sum(Hest[,k,,i])
        H.antlerless.dmu[i,k] <- sum(Hest[2:3,k,1,i]) + sum(Hest[1,k,,i])
        H.yr.bucks.dmu[i,k] <- sum(Hest[2,k,2,i])
        H.ad.bucks.dmu[i,k] <- sum(Hest[3,k,2,i])
      }
    }
    
    # Population growth rate
    for(i in 1:nDMUs){
      for(k in 1:(nyears + npredyrs - 1)){
        lambda.dmu[i,k] <- sum(N[,k+1,,i]) / sum(N[,k,,i])
        logla.dmu[i,k] <- log(lambda.dmu[i,k])
      }
      
      mlam.dmu[i] <- exp((1 / (nyears + npredyrs - 1)) * sum(logla.dmu[i,1:(nyears + npredyrs - 1)]))
    }


    
    } # End model 
    
    
